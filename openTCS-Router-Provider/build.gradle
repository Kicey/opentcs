apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply from: "${rootDir}/gradle/license-headers.gradle"
apply from: "${rootDir}/gradle/publishing-java.gradle"
apply plugin: 'org.kordamp.gradle.stats'

if (!hasProperty('mainClass')) {
  ext.mainClass = 'site.kicey.strategies.provider.router.RunKernel'
}
mainClassName = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

dependencies {
  api project(':openTCS-API-Injection')
  api project(':openTCS-Common')
  api project(':openTCS-CommAdapter-Loopback')
  api project(':openTCS-PeripheralCommAdapter-Loopback')
  api project(':openTCS-Impl-Configuration-cfg4j')
  api project(':openTCS-Kernel-Base')
  api project(':openTCS-Kernel-Extension-Redis')
  api project(':openTCS-Strategies-API')
  api project(':openTCS-Strategies-Dubbo-Base')
  implementation group: 'de.huxhorn.sulky', name: 'de.huxhorn.sulky.ulid', version: '8.3.0'

  implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.5.1'
  runtimeOnly group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.36'
}

distributions {
  main {
    contents {
      from "${sourceSets.main.resources.srcDirs[0]}/org/opentcs/kernel/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'java.security.policy':'file:./config/java.policy',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.cfg4j.reload.interval':'10000'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow', '-Dsun.net.maxDatagramSockets=1024')
  // jvmArgs('-XX:+UnlockExperimentalVMOptions', '-XX:+UseEpsilonGC')
}
